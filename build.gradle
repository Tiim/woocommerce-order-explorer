plugins {
  id "java"
  id 'application'
  id "org.openjfx.javafxplugin" version "0.0.8"
  id 'org.beryx.jlink' version '2.17.1'
}

group 'ch.scbirs.shop'
def desc = 'git describe --tags --long'.execute().text.trim()
if (desc.isEmpty()) {
    desc = 'v0.0.0-0-nogitcommit'
}
println("Git describe: $desc")
def gitversion = desc.split("-").toList()
def gitsha = gitversion.removeLast() // get sha
def commits = gitversion.removeLast() // get commits since tag
def ver = gitversion.join("-").substring(1) // get tag minus first character
def fullver = ver + "+" + commits + "." + gitsha

if (commits != "0") {
    println("Full Version: $fullver")
    version fullver
} else {
    println("Version: $ver")
    version ver
}


mainClassName = 'ch.scbirs.shop.orderexplorer.OrderExplorer'

sourceCompatibility = 1.12 
targetCompatibility = 1.12



repositories {
    mavenCentral()
}

dependencies {
    compile group: "org.openjfx", name: "javafx-base", version: "14"
    compile group: "org.openjfx", name: "javafx", version: "14"
    compile group: "org.openjfx", name: "javafx-controls", version: "13.0.2"
    compile group: "org.openjfx", name: "javafx-graphics", version: "14"
    compile group: "org.openjfx", name: "javafx-swing", version: "14"
    compile group: "org.openjfx", name: "javafx-web", version: "14"
    compile group: "org.openjfx", name: "javafx-fxml", version: "14"


    compile 'com.squareup.okhttp3:okhttp:4.5.0'

    compile 'com.fasterxml.jackson.core:jackson-core:2.10.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.10.3'

    compile 'com.google.guava:guava:28.2-jre'
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.commons:commons-lang3:3.10'

    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.1'

    compile 'org.apache.poi:poi:4.1.2'

    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.18'
    compile 'com.github.dhorions:boxable:1.5'

    compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'

    compile group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0'

    runtime group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.13.1'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.1'

    testCompile group: 'junit', name: 'junit', version: '4.13'
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes 'Main-Class': 'ch.scbirs.shop.orderexplorer.OrderExplorer'
        attributes 'Specification-Version': ver
        attributes 'Implementation-Version': fullver
    }
}

javafx {
    version = "12"
    modules = [
        'javafx.controls',
        'javafx.base',
        'javafx.fxml',
        'javafx.graphics',
        'javafx.web',
        'javafx.swing'
    ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'orderexplorer'
    }
}
