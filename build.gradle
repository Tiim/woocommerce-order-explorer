plugins {
  id "java"
  id 'application'
  id "org.openjfx.javafxplugin" version "0.0.10"
  id 'org.beryx.jlink' version '2.24.2'
}

group 'ch.scbirs.shop'
def desc = 'git describe --tags --long'.execute().text.trim()
if (desc.isEmpty()) {
    desc = 'v0.0.0-0-nogitcommit'
}
println("Git describe: $desc")
def gitversion = desc.split("-").toList()
def gitsha = gitversion.removeLast() // get sha
def commits = gitversion.removeLast() // get commits since tag
def ver = gitversion.join("-").substring(1) // get tag minus first character
def fullver = ver + "." + commits + "." + gitsha

if (commits != "0") {
    println("Full Version: $fullver")
    version fullver
} else {
    println("Version: $ver")
    version ver
}

version ver

defaultTasks 'zipExe'

application.mainModule = 'ch.scbirs.shop.orderexplorer'
mainClassName = 'ch.scbirs.shop.orderexplorer.OrderExplorer'

sourceCompatibility = 1.12 
targetCompatibility = 1.12



repositories {
    mavenCentral()
}

dependencies {
    implementation group: "org.openjfx", name: "javafx-base", version: "15.0.1.0.1"
    implementation group: "org.openjfx", name: "javafx", version: "14"
    implementation group: "org.openjfx", name: "javafx-controls", version: "14.0.1"
    implementation group: "org.openjfx", name: "javafx-graphics", version: "14.0.1"
    implementation group: "org.openjfx", name: "javafx-swing", version: "15.0.1"
    implementation group: "org.openjfx", name: "javafx-web", version: "14.0.1"
    implementation group: "org.openjfx", name: "javafx-fxml", version: "14.0.1"


    implementation 'com.squareup.okhttp3:okhttp:4.9.1'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.11.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.3'

    implementation 'com.google.guava:guava:30.0-jre'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.commons:commons-lang3:3.10'

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3'

    implementation 'org.apache.poi:poi:4.1.2'

    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.21'
    implementation 'com.github.dhorions:boxable:1.5'

    //implementation group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'

    implementation group: 'com.github.zafarkhaja', name: 'java-semver', version: '0.9.0'

    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.13.3'
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.2'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
}

jar {
    from {
        //configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes 'Main-Class': 'ch.scbirs.shop.orderexplorer.OrderExplorer'
        attributes 'Specification-Version': ver
        attributes 'Implementation-Version': fullver
    }
}

javafx {
    version = "12"
    modules = [
        'javafx.controls',
        'javafx.base',
        'javafx.fxml',
        'javafx.graphics',
        'javafx.web',
        'javafx.swing'
    ]
}

jlink {
    moduleName = 'ch.scbirs.shop.orderexplorer'
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    forceMerge "jackson", "log4j"
    launcher {
        name = 'orderexplorer'
    }
    jpackage {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += []
        }
        skipInstaller = true
    }
}

task zipExe(type: Zip) {
    from "$buildDir/jpackage"
    include '*'
    include '**/*' //to include contents of a folder present inside Reports directory
    archiveFileName = 'OrderExplorer-'+ fullver +'.zip'
    destinationDirectory = file("$buildDir")
    dependsOn "jpackage"
}
